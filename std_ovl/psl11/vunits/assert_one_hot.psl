// Accellera Standard V1.6 Open Verification Library (OVL).
// Accellera Copyright (c) 2005-2006. All rights reserved.

vunit assert_one_hot_assert_vunit (assert_one_hot_assert)
{
  default clock = (posedge clk);

  property ASSERT_ONE_HOT_XZ_P = always (
          reset_n && xzcheck_enable ->
          ((test_expr ^ test_expr) == {width{1'b0}})); 

  property ASSERT_ONE_HOT_P = always (
           reset_n ->
           ((test_expr == 0) ? 1'b0 : (test_expr & (test_expr - 1)) == 0));

  A_ASSERT_ONE_HOT_XZ_P:
  assert ASSERT_ONE_HOT_XZ_P
  report "VIOLATION: ASSERT_ONE_HOT Checker Fires : Test expression contains X/Z value";

  A_ASSERT_ONE_HOT_P:
  assert ASSERT_ONE_HOT_P
  report "VIOLATION: ASSERT_ONE_HOT Checker Fires : Test expression contains more or less than 1 asserted bits";
}

vunit assert_one_hot_assume_vunit (assert_one_hot_assume)
{
  default clock = (posedge clk);

  property ASSERT_ONE_HOT_XZ_P = always (
          reset_n && xzcheck_enable ->
          ((test_expr ^ test_expr) == {width{1'b0}}));
                                                                                                                             
  property ASSERT_ONE_HOT_P = always (
           reset_n && (test_expr ^ test_expr == {width{1'b0}})  ->
           ((test_expr == 0) ? 1'b0 : (test_expr & (test_expr - 1)) == 0));
                                                                                                                             
  M_ASSERT_ONE_HOT_XZ_P:
  assume ASSERT_ONE_HOT_XZ_P;
                                                                                                                             
  M_ASSERT_ONE_HOT_P:
  assume ASSERT_ONE_HOT_P;
}

vunit assert_one_hot_cover_vunit (assert_one_hot_cover)
{ 
  default clock = (posedge clk);

  cover_test_expr_change:
  cover {reset_n && prev(reset_n) && (test_expr != prev(test_expr))}
  report "COVERAGE REPORT : ASSERT_ONE_HOT Checker: test_expr_change covered";

  cover_all_one_hots_checked:
  cover {reset_n && rose(one_hots_checked == {width{1'b1}})}
  report "COVERAGE REPORT: ASSERT_ONE_HOT Checker: all_one_hots_checked covered";
}
 
