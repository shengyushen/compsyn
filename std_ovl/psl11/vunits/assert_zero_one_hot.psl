// Accellera Standard V1.6 Open Verification Library (OVL).
// Accellera Copyright (c) 2005-2006. All rights reserved.

vunit assert_zero_one_hot_assert_vunit (assert_zero_one_hot_assert)
{
  default clock = (posedge clk);

  property ASSERT_ZERO_ONE_HOT_XZ_P = always (
          reset_n && xzcheck_enable ->
          ((test_expr ^ test_expr) == {width{1'b0}})); 

  property ASSERT_ZERO_ONE_HOT_P = always (
           reset_n ->
           ((test_expr == 0) ? 1'b1 : (test_expr & (test_expr - 1)) == 0));

  A_ASSERT_ZERO_ONE_HOT_XZ_P:
  assert ASSERT_ZERO_ONE_HOT_XZ_P
  report "VIOLATION: ASSERT_ZERO_ONE_HOT Checker Fires : Test expression contains X/Z value";

  A_ASSERT_ZERO_ONE_HOT_P:
  assert ASSERT_ZERO_ONE_HOT_P
  report "VIOLATION: ASSERT_ZERO_ONE_HOT Checker Fires : Test expression contains more than 1 asserted bits";
}

vunit assert_zero_one_hot_assume_vunit (assert_zero_one_hot_assume)
{
  default clock = (posedge clk);

  property ASSERT_ZERO_ONE_HOT_XZ_P = always (
          reset_n && xzcheck_enable ->
          ((test_expr ^ test_expr) == {width{1'b0}}));
                                                                                                                             
  property ASSERT_ZERO_ONE_HOT_P = always (
           reset_n ->
           ((test_expr == 0) ? 1'b1 : (test_expr & (test_expr - 1)) == 0));
                                                                                                                             
  M_ASSERT_ZERO_ONE_HOT_XZ_P:
  assume ASSERT_ZERO_ONE_HOT_XZ_P;
                                                                                                                             
  M_ASSERT_ZERO_ONE_HOT_P:
  assume ASSERT_ZERO_ONE_HOT_P;
}

vunit assert_zero_one_hot_cover_vunit (assert_zero_one_hot_cover)
{ 
  default clock = (posedge clk);

  cover_test_expr_change:
  cover {prev(reset_n) && reset_n && (test_expr != prev(test_expr))}
  report "COVERAGE REPORT : ASSERT_ZERO_ONE_HOT Checker: test_expr_change covered";

  cover_test_expr_all_zeros:
  cover {reset_n && rose(test_expr == {width{1'b0}})}
  report "COVERAGE REPORT : ASSERT_ZERO_ONE_HOT Checker: test_expr_all_zeros covered";
 
  cover_all_one_hots_checked:
  cover {reset_n && rose(all_one_hots_checked)}
  report "COVERAGE REPORT: ASSERT_ZERO_ONE_HOT Checker: all_one_hots_checked covered";
}
 
