// Accellera Standard V1.6 Open Verification Library (OVL).
// Accellera Copyright (c) 2005-2006. All rights reserved.

vunit assert_time_assert_vunit (assert_time_assert)
{
  default clock = (posedge clk);

  property ASSERT_TIME_P = always (
           { (num_cks > 0) && (ignore_new_start || error_on_new_start) && 
             start_event && !window} |=>  
              {test_expr[*num_cks]} abort(!reset_n) ) ;
  
  property ASSERT_TIME_RESET_ON_START_P = always (
           { (num_cks > 0) && reset_on_new_start && start_event && window} 
           |=> {test_expr[*num_cks]} abort(!reset_n) );

  property ASSERT_TIME_ERR_ON_START_P = never 
           {error_on_new_start && reset_n && start_event && window};

  A_ASSERT_TIME_P:
  assert ASSERT_TIME_P
  report "VIOLATION: ASSERT_TIME Checker Fires: Test expression is not TRUE within specified num_cks cycles from the start_event";

  A_ASSERT_TIME_RESET_ON_START_P:
  assert ASSERT_TIME_RESET_ON_START_P
  report "VIOLATION: ASSERT_TIME Checker Fires: Test expression is not TRUE within specified num_cks cycles from the start_event";

  A_ASSERT_TIME_ERR_ON_START_P:
  assert ASSERT_TIME_ERR_ON_START_P 
  report "VIOLATION: ASSERT_TIME Checker Fires : Illegal start event which has reoccured before completion of current window";
}

vunit assert_time_assume_vunit (assert_time_assume)
{
  default clock = (posedge clk);
 
  property ASSERT_TIME_P = always (
           { (num_cks > 0) && (ignore_new_start || error_on_new_start) &&
             start_event && !window} |=>
             {test_expr[*num_cks]} abort(!reset_n) );

  property ASSERT_TIME_RESET_ON_START_P = always (
           { (num_cks > 0) && reset_on_new_start && start_event && window}
           |=>  {test_expr[*num_cks]} abort(!reset_n) );

  property ASSERT_TIME_ERR_ON_START_P = never
           {error_on_new_start && reset_n && start_event && window};
 
  A_ASSERT_TIME_P:
  assume ASSERT_TIME_P;

  A_ASSERT_TIME_RESET_ON_START_P:
  assume ASSERT_TIME_RESET_ON_START_P;

  A_ASSERT_TIME_ERR_ON_START_P:
  assume ASSERT_TIME_ERR_ON_START_P;
}

vunit assert_time_cover_vunit (assert_time_cover)
{ 
  default clock = (posedge clk);

  cover_window_open:
  cover {reset_n && start_event && !window}
  report "COVERAGE REPORT : ASSERT_TIME Checker: window_open covered";
                                                                                                                                     
  cover_window_close:
  cover {reset_n && window && (window_close && ((!reset_on_new_start) || (!start_event)))}
  report "COVERAGE REPORT : ASSERT_TIME Checker: window_close covered";
                                                                                                                                     
  cover_window_resets:
  cover {reset_n && reset_on_new_start && start_event && window}
  report "COVERAGE REPORT : ASSERT_TIME Checker: window_resets covered";
}
 
