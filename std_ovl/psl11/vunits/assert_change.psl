// Accellera Standard V1.6 Open Verification Library (OVL).
// Accellera Copyright (c) 2005-2006. All rights reserved.

vunit assert_change_assert_vunit (assert_change_assert)
{
  default clock = (posedge clk);

  property ASSERT_CHANGE_P = always (
           ({(num_cks > 0) && (ignore_new_start || error_on_new_start) &&
            start_event && !window} |=>
           {[*0:(num_cks > 0 ? num_cks - 1 : 1)];(test_expr != prev(test_expr))})
           abort(!reset_n));
  
  property ASSERT_CHANGE_RESET_ON_START_P = always (
           ({(num_cks > 0) && reset_on_new_start && start_event} |=>
            {[*0:(num_cks > 0 ? num_cks - 1 : 1)];
             ((test_expr != prev(test_expr)) || start_event)})
           abort(!reset_n));

  property ASSERT_CHANGE_ERR_ON_START_P = always (
           reset_n && error_on_new_start && window -> !start_event);

  A_ASSERT_CHANGE_P:
  assert ASSERT_CHANGE_P
  report "VIOLATION: ASSERT_CHANGE Checker Fires: Test expression did not change value within num_cks cycles after start event";

  A_ASSERT_CHANGE_RESET_ON_START_P:
  assert ASSERT_CHANGE_RESET_ON_START_P
  report "VIOLATION: ASSERT_CHANGE Checker Fires: Start event evaluated TRUE before test expression changed";

  A_ASSERT_CHANGE_ERR_ON_START_P:
  assert ASSERT_CHANGE_ERR_ON_START_P 
  report "VIOLATION: ASSERT_CHANGE Checker Fires : Illegal re-occurance of start event";
}

vunit assert_change_assume_vunit (assert_change_assume)
{
  default clock = (posedge clk);
 
  property ASSERT_CHANGE_P = always (
           ({(num_cks > 0) && (ignore_new_start || error_on_new_start) &&
            start_event && !window} |=>
           {[*0:(num_cks > 0 ? num_cks - 1 : 1)];(test_expr != prev(test_expr))})
           abort(!reset_n));
  
  property ASSERT_CHANGE_RESET_ON_START_P = always (
           ({(num_cks > 0) && reset_on_new_start && start_event} |=>
            {[*0:(num_cks > 0 ? num_cks - 1 : 1)];
             ((test_expr != prev(test_expr)) || start_event)})
           abort(!reset_n));
  
  property ASSERT_CHANGE_ERR_ON_START_P = always (
           reset_n && error_on_new_start && window -> !start_event);
 
  M_ASSERT_CHANGE_P:
  assume ASSERT_CHANGE_P;
                                                                                                                                                       
  M_ASSERT_CHANGE_RESET_ON_START_P:
  assume ASSERT_CHANGE_RESET_ON_START_P;
                                                                                                                                                       
  M_ASSERT_CHANGE_ERR_ON_START_P:
  assume ASSERT_CHANGE_ERR_ON_START_P;
}

vunit assert_change_cover_vunit (assert_change_cover)
{ 
  default clock = (posedge clk);

  cover_window_open:
  cover {reset_n && start_event && !window}
  report "COVERAGE REPORT : ASSERT_CHANGE Checker: window_open covered";
  
  cover_window_close:
  cover {reset_n && window && (window_close && ((!reset_on_new_start) || (!start_event)))}
  report "COVERAGE REPORT : ASSERT_CHANGE Checker: window_close covered";
  
  cover_window_resets:
  cover {reset_n && reset_on_new_start && start_event && window}
  report "COVERAGE REPORT : ASSERT_CHANGE Checker: window_resets covered";
}
 
